# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                MacOSX.sdk
version             10.15
categories          devel
platforms           macosx
maintainers         {gcenx @gcenx}

license             Restrictive Nomirror

homepage            https://developer.apple.com/macos/

subport MacOSX10.15.sdk {
    set macos_name      {macOS Catalina}
    set xcode_vers      12
    version             10.15
    revision            0
    
    homepage            https://developer.apple.com/documentation/macos-release-notes/macos-catalina-10_15-release-notes
    
    checksums           rmd160  580ec054e9d38fe846ce597a0f775d3fc46526b5 \
                        sha256  bb548125ebf5cf4ae6c80d226f40ad39e155564ca78bc00554a2e84074d0180e \
                        size    41636648
}

subport MacOSX10.14.sdk {
    set macos_name      {macOS Mojave}
    set xcode_vers      10.3
    version             10.14
    revision            0
    
    homepage            https://developer.apple.com/documentation/macos-release-notes/macos-mojave-10_14-release-notes
    
    checksums           rmd160  7684478f171e30842274fafb784b91a8e79485dd \
                        sha256  e0a9747ae9838aeac70430c005f9757587aa055c690383d91165cf7b4a80401d \
                        size    31704241
}

subport MacOSX10.13.sdk {
    set macos_name      {macOS High Sierra}
    set xcode_vers      9.3
    version             10.13
    revision            0
    
    homepage            https://developer.apple.com/library/archive/releasenotes/General/RN-macOSSDK-10.13/
    
    checksums           rmd160  255ee173939065c9d1b7433f287163cdd00e0033 \
                        sha256  27943fb63c35e262b2da1cb4cc60d7427702a40caf20ec15c9d773919c6e409b \
                        size    32183283
    
    depends_build       port:MacOSX10.11.sdk
    
    # QuickTime.framework was deprecated, lets replace it with a working copy from MacOSX10.11.sdk
    post-destroot {
        file delete -force ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/QuickTime.framework
        file copy   ${sdk_dir}/MacOSX10.11.sdk/System/Library/Frameworks/QuickTime.framework ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks
    }
}

subport MacOSX10.12.sdk {
    set macos_name      {macOS Sierra}
    set xcode_vers      8.3
    version             10.12
    revision            0
    
    checksums           rmd160  282f73e728c72406b936ce6a658786e63c7897ba \
                        sha256  fd4e1151056f34f76b5930cb45b9cd859414acbc3f6529c0c0ecaaf2566823ff \
                        size    36301027
    
    depends_build       port:MacOSX10.11.sdk
    
    # QuickTime.framework was deprecated, lets replace it with a working copy from MacOSX10.11.sdk
    post-destroot {
        file delete -force ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/QuickTime.framework
        file copy   ${sdk_dir}/MacOSX10.11.sdk/System/Library/Frameworks/QuickTime.framework ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks
        
        # AMDil_r700.dylib is not a valid dylib and also links to
        # /System/Library/Frameworks/OpenGL.framework/Versions/A/Libraries/Legacy/libLLVMContainer.dylib
        # The following dylib files were removed from Xcode_9.3 MacOSX10.13.sdk
        file delete ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/OpenCL.framework/Versions/A/Libraries/AMDil_r700.dylib
        file delete ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/OpenCL.framework/Versions/A/Libraries/AMDil.dylib
        file delete ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/OpenCL.framework/Versions/A/Libraries/IGIL.dylib
        
        # These don't appear until OS X 10.8
        if {${os.major} <= 11} {
            file delete -force ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/PrivateFrameworks/CalendarFoundation.framework
            file delete -force ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/PrivateFrameworks/CalendarAgent.framework
        }
    }
}

subport MacOSX10.11.sdk {
    set macos_name      {OS X El Capitan}
    set xcode_vers      7.3
    version             10.11
    revision            0
    
    checksums           rmd160  174d1ecd797d50eea7a067e8396ae12dbc4d0b26 \
                        sha256  2e28c2eeb716236d89ea3d12a228e291bdab875c50f013a909f3592007f21484 \
                        size    36006642
    
    post-destroot {
        # AMDil_r700.dylib is not a valid dylib and also links to
        # /System/Library/Frameworks/OpenGL.framework/Versions/A/Libraries/Legacy/libLLVMContainer.dylib
        # The following dylib files were removed from Xcode_9.3 MacOSX10.13.sdk
        file delete ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/OpenCL.framework/Versions/A/Libraries/AMDil_r700.dylib
        file delete ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/OpenCL.framework/Versions/A/Libraries/AMDil.dylib
        file delete ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/Frameworks/OpenCL.framework/Versions/A/Libraries/IGIL.dylib
        
        # These don't appear until OS X 10.8
        if {${os.major} <= 11} {
            file delete -force ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/PrivateFrameworks/CalendarFoundation.framework
            file delete -force ${destroot}${sdk_dir}/MacOSX${version}.sdk/System/Library/PrivateFrameworks/CalendarAgent.framework
        }
    }
}

# All other SDKs contain actual real dylib files and not tbd stubs causing macports to incorrectly believe the installed SDK is broken

# You'd think this would be in Tcl or MacPorts base already.
# http://wiki.tcl.tk/4884
proc map {prefix list} {
    set result {}
    foreach item ${list} {
        lappend result [{*}${prefix} ${item}]
    }
    return ${result}
}

# You'd think this would be in MacPorts base already.
proc shell_quote {path} {
    # This is terrible, but good enough for this port.
    return "\"${path}\""
}

proc check_disk_space {path requested_bytes} {
    set requested_mbytes [expr {${requested_bytes} / 1024 / 1024}]
    set df_k [lindex [split [exec df -k ${path}] \n] end]
    set volume_mbytes [expr {[lindex ${df_k} 1] / 1024}]
    set free_mbytes [expr {[lindex ${df_k} 3] / 1024}]
    set needed_mbytes [expr {ceil((1.05 * ${requested_mbytes} + 0.01 * ${volume_mbytes}) - ${free_mbytes})}]
    ui_debug "${requested_mbytes} MiB requested; ${free_mbytes} MiB free"
    if {${needed_mbytes} > 0} {
        ui_error "The disk is nearly full. Please free up an additional ${needed_mbytes} MiB."
        return -code error "insufficient disk space"
    }
}

set sdk_dir                 ${prefix}/Developer/SDKs
set sdk_dir2                /Library/Developer/CommandLineTools/SDKs

use_configure               no

destroot.violate_mtree      yes

use_bzip2                   yes

distname                    MacOSX${version}

# GitHub requires TLS v1.2
if {${os.major} > 15} {
    master_sites                https://github.com/alexey-lysiuk/macos-sdk/releases/download/${version}/
} else {
    depends_build-append        port:curl
    
    fetch {
            # Manually download the file(s) if they don't exist
            if {![file exists ${portdbpath}/distfiles/${dist_subdir}/MacOSX${version}.tar.bz2 ]} {
                system -W /bin "cd ${portdbpath}/distfiles/${dist_subdir}/ && ${prefix}/bin/curl -LO https://github.com/alexey-lysiuk/macos-sdk/releases/download/${version}/MacOSX${version}.tar.bz2"
            }
    }
}

build {}

pre-destroot {
    xinstall -d ${destroot}${sdk_dir}
    xinstall -d ${destroot}${sdk_dir2}
}

if {${subport} eq ${name}} {
    revision            0
    description         latest macOS SDK
    long_description    This port installs a link to the latest macOS SDK.
    distfiles
    supported_archs     noarch
    depends_run-append  port:MacOSX${version}.sdk
    destroot {
        ln -s MacOSX${version}.sdk ${destroot}${sdk_dir}/MacOSX.sdk
    }
} else {
    set macos_version   [lindex [split ${version} -] 0]
    set macos_build     [lindex [split ${version} -] 1]
    set macos_major     [join [lrange [split ${macos_version} .] 0 1] .]
    if {[vercmp ${macos_version} 10.14] >= 0} {
        supported_archs x86_64
    } else {
        supported_archs i386 x86_64
    }
    description         ${macos_name} v${macos_version} (${macos_build}) SDK
    long_description    This port installs the ${macos_name} v${macos_version} (${macos_build}) SDK.
    destroot {
        move ${worksrcpath}.sdk ${destroot}${sdk_dir}
        
        # Check if the SDK exits before adding a symlink
        # can't use ${developer_dir}/SDKs as that could return Xcode
        if {![file exists ${sdk_dir2}/MacOSX${version}.sdk ]} {
            ln -sf ${sdk_dir}/MacOSX${version}.sdk ${destroot}${sdk_dir2}/MacOSX${version}.sdk
        }
    }
}

if {[llength ${supported_archs}] > 1} {
    variant universal {}
    default_variants +universal
    variant_set universal
}

livecheck.type      none
