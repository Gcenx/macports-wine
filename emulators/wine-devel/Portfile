# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                  1.0
PortGroup                   github 1.0
PortGroup                   muniversal 1.1

# Keep the wine-stable, wine-devel and wine-crossover portfiles as similar as possible.

github.setup                wine-mirror wine 11.0-rc4 wine-
github.tarball_from         archive
name                        wine-devel
conflicts                   wine-stable wine-staging wine-crossover
revision                    0
platforms                   {darwin > 15}
set branch                  [lindex [split ${version} .] 0].x
license                     LGPL-2.1+
categories                  emulators
supported_archs             i386 x86_64
maintainers                 {@Gcenx}
homepage                    https://www.winehq.org
dist_subdir                 wine

description \
    A Windows API implementation

long_description \
    Wine is a compatibility layer capable of running \
    Windows applications. \
    Instead of simulating internal Windows logic like \
    a virtual machine or emulator, Wine translates \
    Windows API calls into POSIX calls on-the-fly, \
    eliminating the performance and memory penalties \
    of other methods and allowing you to cleanly \
    integrate Windows applications into your desktop.

checksums \
    ${distname}${extract.suffix} \
    rmd160  2c99e7c33c2e60c8e9a4ab22bcbd1001272cf87a \
    sha256  878f9d967273df24d7464fb2fdc420027519b0bee83e36ec48d3666b452426d3 \
    size    53775727

depends_build \
    port:bison \
    bin:flex:flex \
    port:gettext \
    port:mingw-w64 \
    path:bin/pkg-config:pkgconfig

depends_lib \
    port:freetype \
    port:gettext-runtime \
    port:gnutls-devel \
    port:libinotify \
    port:libpcap \
    port:libsdl2

depends_run \
    port:mingw-w64-wine-gecko-2.47.4 \
    port:mingw-w64-wine-mono-10.4.1

post-extract {
    # https://gitlab.winehq.org/wine/wine/-/commit/c7a97b5d5d56ef00a0061b75412c6e0e489fdc99
    reinplace -q "/PKG_CONFIG_LIBDIR/d"  ${worksrcpath}/configure
    reinplace -q "/PKG_CONFIG_LIBDIR/d"  ${worksrcpath}/configure.ac
}

patch.pre_args-replace -p0 -p1

# Upstream bug fixes here

# Rosetta2 hacks here
patchfiles-append \
    0001-ntdll-CW-HACK-18947.patch \
    0002-ntdll-CW-HACK-20186.patch \
    0003-wow64cpu-CW-HACK-20760.patch \
    0005-ntdll-CW-HACK-23427.patch \
    0006-ntdll-CW-Hack-24256.patch \
    0007-ntdll-CW-HACK-24265.patch \
    0008-ntdll-CW-HACK-24945.patch \
    0009-ntdll-CW-HACK-25719.patch \
    0010-ntdll-HACK-Winehq-Bug-56441.patch

# Steam hacks here
patchfiles-append \
    0011-kernelbase-HACK-Add-hack_append_command_line.patch \
    0012-kernelbase-CW-HACK-13322-17315-21883.patch

# DXMT hacks here
patchfiles-append \
    0013-ntdll-CW-HACK-22435.patch \
    0014-winemac-DXMT-export.patch \
    0015-winemetal-new-stub.patch

configure.checks.implicit_function_declaration.whitelist-append \
    __clear_cache \
    fallocate \
    gethostbyaddr_r \
    gethostbyname_r \
    sched_setaffinity

# wine requires the program specified in INSTALL to create intermediate
# directories; /usr/bin/install doesn't.
# http://bugs.winehq.org/show_bug.cgi?id=35310
configure.install \
    ${worksrcpath}/tools/install-sh

configure.args.x86_64 \
    --enable-win64

configure.args \
    --without-alsa \
    --without-capi \
    --with-coreaudio \
    --with-cups \
    --without-dbus \
    --without-ffmpeg \
    --without-fontconfig \
    --with-freetype \
    --with-gettext \
    --without-gettextpo \
    --without-gphoto \
    --with-gnutls \
    --without-gssapi \
    --without-gstreamer \
    --with-inotify \
    --without-krb5 \
    --with-mingw \
    --without-netapi \
    --with-opencl \
    --without-opengl \
    --without-oss \
    --with-pcap \
    --with-pcsclite \
    --with-pthread \
    --without-pulse \
    --without-sane \
    --with-sdl \
    --without-udev \
    --with-unwind \
    --without-usb \
    --without-v4l2 \
    --without-vulkan \
    --without-wayland \
    --without-x

configure.args-append \
    --disable-tests \
    --disable-winebth_sys \
    --disable-winemenubuilder

configure.env-append        ac_cv_lib_soname_vulkan=

# We need to tell the linker to add MacPorts to the rpath stack.
configure.ldflags-append    -Wl,-rpath,${compiler.library_path}

# wine requires clang >= 3.8
# FSF GCC cannot compile code using Apple's "blocks" language extensions
compiler.blacklist-append   {*gcc*} {clang < 800} {macports-clang-3.*}

subport wine-staging {
    conflicts               wine-stable wine-devel wine-crossover
    set staging_version     ${version}
    set wine_staging_distfile v${staging_version}${extract.suffix}
    distfiles-append        ${wine_staging_distfile}:stagingsource
    master_sites-append     https://github.com/wine-staging/wine-staging/archive/:stagingsource
    worksrcdir              wine-${distname}
    extract.rename          no

    checksums-append \
        ${wine_staging_distfile} \
        rmd160  2c9e58b6432bbeaee305b2896cb2757b1d8c15e2 \
        sha256  0b003f7eff081badd053a26aead48360efbfc70a7f05a273764ec07348871076 \
        size    9323254

    depends_patch-append    port:autoconf

    # Battle.net hacks here
    patchfiles-append       1001-kernelbase-CW-HACK-19610.patch

    set py_ver              3.13
    set py_ver_nodot        [string map {. {}} ${py_ver}]
    depends_patch-append    port:python${py_ver_nodot}

    # Applying staging before other patchfiles
    pre-patch {
        system -W ${worksrcpath} \
            "${frameworks_dir}/Python.framework/Versions/${py_ver}/bin/python3 ${workpath}/wine-staging-${staging_version}/staging/patchinstall.py --all"
    }
}

variant gphoto description "Build ${subport} with support for digital cameras" {
    depends_lib-append      port:libgphoto2
    configure.args-replace  --without-gphoto --with-gphoto
}

variant gstreamer description "Build ${subport} with GStreamer, for multimedia support" {
    depends_lib-append      path:Library/Frameworks/GStreamer.framework:gstreamer.framework
    configure.args-replace  --without-ffmpeg --with-ffmpeg
    configure.args-replace  --without-gstreamer --with-gstreamer

    pre-configure {
        configure.env-append    "FFMPEG_CFLAGS=[exec ${frameworks_dir}/GStreamer.framework/Commands/pkg-config --cflags libavutil libavformat libavcodec]"
        configure.env-append    "FFMPEG_LIBS=[exec ${frameworks_dir}/GStreamer.framework/Commands/pkg-config --libs libavutil libavformat libavcodec]"
        configure.env-append    "GSTREAMER_CFLAGS=[exec ${frameworks_dir}/GStreamer.framework/Commands/pkg-config --cflags gstreamer-1.0 gstreamer-video-1.0 gstreamer-audio-1.0 gstreamer-tag-1.0]"
        configure.env-append    "GSTREAMER_LIBS=[exec ${frameworks_dir}/GStreamer.framework/Commands/pkg-config --libs gstreamer-1.0 gstreamer-video-1.0 gstreamer-audio-1.0 gstreamer-tag-1.0]"
    }

    post-configure {
        reinplace -q "s|Versions/1.0/lib/pkgconfig/../../lib|Libraries|g" ${worksrcpath}/config.status
    }
}

variant kerberos description "Build ${subport} with Kerberos, for network authentication protocol support" {
    depends_lib-append      port:kerberos5
    configure.args-replace  --without-krb5 --with-krb5
}

if {${os.major} >= 24} {
    # https://gitlab.winehq.org/wine/wine/-/merge_requests/5935#note_74758
    macosx_deployment_target    14.0
}

if {${os.major} >= 19} {
    depends_lib-append          path:lib/libMoltenVK.dylib:MoltenVK-latest
    configure.args.x86_64-append --enable-archs=i386,x86_64
    configure.args-replace      --without-vulkan --with-vulkan
    default_variants            +gstreamer
}

if {${os.major} < 19} {
    # CodeWeavers only tests i386 using MacOSX10.13.sdk
    configure.sdk_version       10.13
    if {${configure.sdkroot} != "${developer_dir}/SDKs/MacOSX${configure.sdk_version}.sdk"} {
        depends_build-append    port:MacOSX${configure.sdk_version}.sdk
        configure.sdkroot       ${prefix}/Developer/SDKs/MacOSX${configure.sdk_version}.sdk
    }

    default_variants            +universal

    if {${universal_possible} && [variant_isset universal]} {
        configure.args.i386     --with-wine64=${workpath}/${worksrcdir}-x86_64

        notes-append "
            \n
            Wine supports both 32-bit and 64-bit now. It is compatible with your\
            existing 32-bit wine prefix, but it will now default to 64-bit when you\
            create a new wine prefix. The architecture can be selected using the\
            WINEARCH environment variable which can be set to either \"win32\" or\
            \"win64\".
            \n
            To create a new pure 32-bit prefix, you can run:
                \$ WINEARCH=win32 WINEPREFIX=~/.wine32 winecfg
            \n
            See the Wine FAQ for details: https://wiki.winehq.org/FAQ#Wineprefixes
        "
    } else {
        supported_archs         i386
    }
}

triplet.add_build           cross
triplet.add_host            none

# macports-base injects a number of flags that now propagate to mingw-gcc this causes the build to fail since, wine-7.21
# https://gitlab.winehq.org/wine/wine/-/commit/b1f59bc679a8c2dea18a6789a5b9b1a1ae825129
compiler.limit_flags        yes
muniversal.arch_flag        no
muniversal.arch_compiler    yes
configure.ldflags-delete    -L${compiler.library_path}
configure.optflags          -O2
configure.env-append        "CROSSCFLAGS=${configure.optflags}"

# Were only installing wine not the development files
destroot.target             install-lib

post-destroot {
    set docdir ${prefix}/share/doc/${subport}
    xinstall -d ${destroot}${docdir}
    xinstall -m 0644 -W ${worksrcpath} \
        ANNOUNCE.md \
        AUTHORS \
        COPYING.LIB \
        LICENSE \
        README.md \
        ${destroot}${docdir}

    if {[variant_isset gstreamer]} {
        system -W ${destroot}${compiler.library_path}/wine/x86_64-unix "/usr/bin/install_name_tool -delete_rpath ${compiler.library_path} winedmo.so"
        system -W ${destroot}${compiler.library_path}/wine/x86_64-unix "/usr/bin/install_name_tool -delete_rpath ${compiler.library_path} winegstreamer.so"
        system -W ${destroot}${compiler.library_path}/wine/x86_64-unix "/usr/bin/codesign --force --sign - winedmo.so"
        system -W ${destroot}${compiler.library_path}/wine/x86_64-unix "/usr/bin/codesign --force --sign - winegstreamer.so"
    }
}
